PREFIX knora-api: <http://api.knora.org/ontology/knora-api/simple/v2#>
PREFIX roud-oeuvres: <http://www.knora.org/ontology/0112/roud-oeuvres#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    
    ##################### MAIN PUBLICATION #####################
    ## main publication data
    ?mainPub a ?mainPubType ;
            roud-oeuvres:publicationHasTitle ?mainPubTitle ;
            roud-oeuvres:publicationHasDate ?mainPubDate ;
            roud-oeuvres:isPublishedInPeriodical ?mainPubPeriodical ;
            roud-oeuvres:publicationHasPhoto ?mainPubPhoto ;
            roud-oeuvres:hasPublisher ?mainPubPublisher .
    
    ## dossier main pub
    ?dossierMainPub a ?dossierMainPubType ;
                    roud-oeuvres:geneticDossierResultsInPublication ?mainPub .
    
    ## avant-textes of main pub
    ?atMainPub  a ?atMainPubType ;
                roud-oeuvres:msIsAvantTextInGeneticDossier ?dossierMainPub ;
                roud-oeuvres:manuscriptHasTitle ?titleAtMainPub ;
                roud-oeuvres:manuscriptIsInArchive ?archiveAtMainPub ;
                roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtMainPub ;
                #  (only interesting if diary) ?atMainPub roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtMainPub .
                roud-oeuvres:hasGeneticStage ?geneticStageAtMainPub .        
    
    
    ##################### PUBLICATION REUSED IN MAIN PUB AND ITS NETWORK #####################
    ## publication reused in main pub (if article)
    # CREA PROBLEMI ... ?pubReusedInMainPub a ?pubReusedInMainPubType .
    ?pubReusedInMainPub roud-oeuvres:publicationIsReusedInDossier ?dossierMainPub ;
                        roud-oeuvres:publicationHasPhoto ?pubReusedInMainPubPhoto ;
                        roud-oeuvres:publicationHasTitle ?pubReusedInMainPubTitle ;
                        roud-oeuvres:publicationHasDate ?pubReusedInMainPubDate ;
                        roud-oeuvres:isPublishedInPeriodical ?pubReusedInMainPubPeriodical .
    
    ## dossier pub reused
    ?dossierPubReused   a ?dossierPubReusedType ;
                        roud-oeuvres:geneticDossierResultsInPublication ?pubReusedInMainPub .
    
    ## avant-textes of pub reused
    ?atPubReused a ?atPubReusedType ;
                roud-oeuvres:msIsAvantTextInGeneticDossier ?dossierPubReused ;
                roud-oeuvres:manuscriptHasTitle ?titleAtPubReused ;
                roud-oeuvres:manuscriptIsInArchive ?archiveAtPubReused ;
                roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtPubReused ;
                # (only interesting if diary) ?atPubReused roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtPubReused . 
                roud-oeuvres:hasGeneticStage ?geneticStageAtPubReused .    
   
    ## diary entries reused in pub reused
    ?diaryReusedInPubReused a ?diaryReusedInPubReusedType ;
                            roud-oeuvres:msIsReusedInDossier ?dossierPubReused ;
                            roud-oeuvres:manuscriptHasTitle ?titleDiaryReusedInPubReused ;
                            roud-oeuvres:manuscriptIsInArchive ?archiveDiaryReusedInPubReused ;
                            roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDiaryReusedInPubReused ;
                            roud-oeuvres:manuscriptHasEditorialSet ?editorialSetDiaryReusedInPubReused .
                            # (always "note" for diary) ?diaryReusedInPubReused roud-oeuvres:hasGeneticStage ?geneticStageDiaryReusedInPubReused .   
    
    
    ##################### PARTS OF MAIN PUBLICATION AND THEIR NETWORKS #####################
    ## pub parts
    ?mainPubPart a ?mainPubPartType ;
             roud-oeuvres:pubPartIsPartOf ?mainPub ;
             roud-oeuvres:pubPartHasTitle ?mainPubPartTitle ;
             roud-oeuvres:pubPartHasNumber ?mainPubPartNumber .
    
    ## pub parts dossiers
    ?dossierPartMainPub a ?dossierParMainPubtType ;
            roud-oeuvres:geneticDossierPartIsPartOfGeneticDossier ?dossierMainPub ;
            roud-oeuvres:geneticDossierPartResultsInPubPart ?mainPubPart .
    
    ## avant-textes of main pub parts
    ?atMainPubPart  a ?atMainPubPartType ;
                    roud-oeuvres:msIsAvantTextInGeneticDossier ?dossierPartMainPub ;
                    roud-oeuvres:manuscriptHasTitle ?titleAtMainPubPart ;
                    roud-oeuvres:manuscriptIsInArchive ?archiveAtMainPubPart ;
                    roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtMainPubPart ;
                    #  (only interesting if diary) roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtMainPub .
                    roud-oeuvres:hasGeneticStage ?geneticStageAtMainPubPart . 
    
    ## diary entries (ms) reused in main pub parts
    ?diaryReusedInPubPart   a ?diaryReusedInPubPartType ;
                            roud-oeuvres:msIsReusedInDossierPart ?dossierPartMainPub ;
                            roud-oeuvres:manuscriptHasTitle ?titleDiaryReusedInPubPart ;
                            roud-oeuvres:manuscriptIsInArchive ?archiveDiaryReusedInPubPart ;
                            roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDiaryReusedInPubPart ;
                            roud-oeuvres:manuscriptHasEditorialSet ?editorialSetDiaryReusedInPubPart .
    
    ## diary entries (msPart) reused in main pub parts
    ?dReusedInPubPart   a ?dReusedInPubPartType ;
                        roud-oeuvres:msPartIsReusedInDossierPart ?dossierPartMainPub ;
                        roud-oeuvres:msPartHasTitle ?titleDReusedInPubPart ;
                        roud-oeuvres:msPartHasNumber ?numberDReusedInPubPart ;
                        roud-oeuvres:msPartIsPartOf ?dReusedInPubPartMs .
    ?dReusedInPubPartMs roud-oeuvres:manuscriptHasTitle ?titleDReusedInPubPartMs ;
                        roud-oeuvres:manuscriptIsInArchive ?archiveDReusedInPubPartMs ;
                        roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDReusedInPubPartMs .
    
    ## publication reusing main pub part
    ?mainPubPart roud-oeuvres:pubPartIsReusedInDossier ?dossierPubReusingMainPubPart .
    ?dossierPubReusingMainPubPart roud-oeuvres:geneticDossierResultsInPublication ?pubReusingMainPubPart .
    ?pubReusingMainPubPart  roud-oeuvres:publicationHasTitle ?pubReusingMainPubPartTitle ;
                            roud-oeuvres:publicationHasDate ?pubReusingMainPubPartDate ;
                            roud-oeuvres:isPublishedInPeriodical ?pubReusingMainPubPartPeriodical ;
                            roud-oeuvres:publicationHasPhoto ?pubReusingMainPubPartPhoto .
    
    
    
    ##################### RE-PUBLICATION #####################
    ?mainPub roud-oeuvres:publicationIsRepublishedDossier ?mainPubRepubDossier .
    ?mainPubRepubDossier roud-oeuvres:geneticDossierResultsInPublication ?mainPubRepub .
    ?mainPubRepub   a ?mainPubRepubType ;
                    roud-oeuvres:publicationHasTitle ?mainPubRepubTypeTitle ;
                    roud-oeuvres:publicationHasDate ?mainPubRepubTypeDate ;
                    roud-oeuvres:publicationHasPhoto ?mainPubRepubPhoto .
    
    
    
    
} WHERE {
    
    FILTER(?mainPub = <http://rdfh.ch/0112/K2OVZydJS42GUsMJvKhP-A>)
    
    ##################### MAIN PUBLICATION #####################
    ## main publication data
    ?mainPub a ?mainPubType .
    # to avoid blank nodes
    FILTER(STRSTARTS(str(?mainPubType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
    ?mainPub roud-oeuvres:publicationHasTitle ?mainPubTitleIRI .
    ?mainPubTitleIRI knora-base:valueHasString ?mainPubTitle .
    ?mainPub roud-oeuvres:publicationHasDate ?mainPubDateIRI .
    ?mainPubDateIRI knora-base:valueHasString ?mainPubDate .
    # if article
    optional {
        ?mainPub roud-oeuvres:isPublishedInPeriodical ?mainPubPeriodicalIRI .
        ?mainPubPeriodicalIRI roud-oeuvres:periodicalHasTitle ?mainPubPeriodicalTitleIRI .
        ?mainPubPeriodicalTitleIRI knora-base:valueHasString ?mainPubPeriodical .
    }
    # if book
    optional {
        ?mainPub roud-oeuvres:hasPublisher ?mainPubPublisherIRI .
        ?mainPubPublisherIRI roud-oeuvres:publisherHasName ?mainPubPublisherNameIRI .
        ?mainPubPublisherNameIRI knora-base:valueHasString ?mainPubPublisher .
    }
    optional {
        ?mainPub roud-oeuvres:publicationHasPhoto ?mainPubPhoto .
    }
        
    ## dossier of main pub
    ?dossierMainPub a ?dossierMainPubType .
    # to avoid blank nodes
    FILTER(STRSTARTS(str(?dossierMainPubType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
    ?dossierMainPub roud-oeuvres:geneticDossierResultsInPublication ?mainPub .
    # following two lines can be commented out
    ?dossierMainPub knora-base:isDeleted ?bool .
    FILTER(?bool = false)
    
    ## avant-textes of main pub
    optional {
        ?atMainPub a ?atMainPubType .
        # to avoid blank nodes
        FILTER(STRSTARTS(str(?atMainPubType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
        ?atMainPub roud-oeuvres:msIsAvantTextInGeneticDossier ?dossierMainPub .
        ?atMainPub roud-oeuvres:manuscriptHasTitle ?titleAtMainPubIRI .
        ?titleAtMainPubIRI knora-base:valueHasString ?titleAtMainPub .
        ?atMainPub roud-oeuvres:manuscriptIsInArchive ?archiveAtMainPubListIRI .
        ?archiveAtMainPubListIRI knora-base:valueHasListNode ?archiveAtMainPubIRI .
        ?archiveAtMainPubIRI rdfs:label ?archiveAtMainPub .
        FILTER(LANG(?archiveAtMainPub) = "en")
        ?atMainPub roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtMainPubIRI .
        ?shelfmarkAtMainPubIRI knora-base:valueHasString ?shelfmarkAtMainPub .
        ?atMainPub roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtMainPubListIRI .
        ?editorialSetAtMainPubListIRI knora-base:valueHasListNode ?editorialSetAtMainPubIRI .
        ?editorialSetAtMainPubIRI rdfs:label ?editorialSetAtMainPub .
        FILTER(LANG(?editorialSetAtMainPub) = "en")
        ?atMainPub roud-oeuvres:hasGeneticStage ?geneticStageAtMainPubListIRI .
        ?geneticStageAtMainPubListIRI knora-base:valueHasListNode ?geneticStageAtMainPubIRI .
        ?geneticStageAtMainPubIRI rdfs:label ?geneticStageAtMainPub .
        FILTER(LANG(?geneticStageAtMainPub) = "en")
    }
            
            
            
            
    
    ##################### PUBLICATION REUSED IN MAIN PUB AND ITS NETWORK #####################
    ## publication reused in main pub
    optional {
        ?pubReusedInMainPub roud-oeuvres:publicationIsReusedInDossier ?dossierMainPub .
        ?pubReusedInMainPub roud-oeuvres:publicationHasTitle ?pubReusedInMainPubTitleIRI .
        ?pubReusedInMainPubTitleIRI knora-base:valueHasString ?pubReusedInMainPubTitle .
        ?pubReusedInMainPub roud-oeuvres:publicationHasDate ?pubReusedInMainPubDateIRI .
        ?pubReusedInMainPubDateIRI knora-base:valueHasString ?pubReusedInMainPubDate .
        ?pubReusedInMainPub roud-oeuvres:isPublishedInPeriodical ?pubReusedInMainPubPeriodicalIRI .
        ?pubReusedInMainPubPeriodicalIRI roud-oeuvres:periodicalHasTitle ?pubReusedInMainPubPeriodicalTitleIRI .
        ?pubReusedInMainPubPeriodicalTitleIRI knora-base:valueHasString ?pubReusedInMainPubPeriodical .
        optional {
            ?pubReusedInMainPub roud-oeuvres:publicationHasPhoto ?pubReusedInMainPubPhoto .
        }
        # following creates PROBLEMS ...
        # ?pubReusedInMainPub a ?pubReusedInMainPubType .
        # to avoid blank nodes
        # FILTER(STRSTARTS(str(?pubReusedInMainPubType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
        
        ## dossier pub reused
        optional {
            ?dossierPubReused a ?dossierPubReusedType .
            # to avoid blank nodes
            FILTER(STRSTARTS(str(?dossierPubReusedType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
            ?dossierPubReused roud-oeuvres:geneticDossierResultsInPublication ?pubReusedInMainPub .
            ## avant-textes of pub reused
            optional {
                ?atPubReused a ?atPubReusedType .
                # to avoid blank nodes
                FILTER(STRSTARTS(str(?atPubReusedType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
                ?atPubReused roud-oeuvres:msIsAvantTextInGeneticDossier ?dossierPubReused .
                ?atPubReused roud-oeuvres:manuscriptHasTitle ?titleAtPubReusedIRI .
                ?titleAtPubReusedIRI knora-base:valueHasString ?titleAtPubReused .
                ?atPubReused roud-oeuvres:manuscriptIsInArchive ?archiveAtPubReusedListIRI .
                ?archiveAtPubReusedListIRI knora-base:valueHasListNode ?archiveAtPubReusedIRI .
                ?archiveAtPubReusedIRI rdfs:label ?archiveAtPubReused .
                FILTER(LANG(?archiveAtPubReused) = "en")
                ?atPubReused roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtPubReusedIRI .
                ?shelfmarkAtPubReusedIRI knora-base:valueHasString ?shelfmarkAtPubReused .
                ?atPubReused roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtPubReusedListIRI .
                ?editorialSetAtPubReusedListIRI knora-base:valueHasListNode ?editorialSetAtPubReusedIRI .
                ?editorialSetAtPubReusedIRI rdfs:label ?editorialSetAtPubReused .
                FILTER(LANG(?editorialSetAtPubReused) = "en")
                ?atPubReused roud-oeuvres:hasGeneticStage ?geneticStageAtPubReusedListIRI .
                ?geneticStageAtPubReusedListIRI knora-base:valueHasListNode ?geneticStageAtPubReusedIRI .
                ?geneticStageAtPubReusedIRI rdfs:label ?geneticStageAtPubReused .
                FILTER(LANG(?geneticStageAtPubReused) = "en")
            }
            ## diary entries reused in pub reused
            optional {
                ?diaryReusedInPubReused a ?diaryReusedInPubReusedType .
                # to avoid blank nodes
                FILTER(STRSTARTS(str(?diaryReusedInPubReusedType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
                ?diaryReusedInPubReused roud-oeuvres:msIsReusedInDossier ?dossierPubReused .
                ?diaryReusedInPubReused roud-oeuvres:manuscriptHasTitle ?titleDiaryReusedInPubReusedIRI .
                ?titleDiaryReusedInPubReusedIRI knora-base:valueHasString ?titleDiaryReusedInPubReused .
                ?diaryReusedInPubReused roud-oeuvres:manuscriptIsInArchive ?archiveDiaryReusedInPubReusedListIRI .
                ?archiveDiaryReusedInPubReusedListIRI knora-base:valueHasListNode ?archiveDiaryReusedInPubReusedIRI .
                ?archiveDiaryReusedInPubReusedIRI rdfs:label ?archiveDiaryReusedInPubReused .
                FILTER(LANG(?archiveDiaryReusedInPubReused) = "en")
                ?diaryReusedInPubReused roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDiaryReusedInPubReusedIRI .
                ?shelfmarkDiaryReusedInPubReusedIRI knora-base:valueHasString ?shelfmarkDiaryReusedInPubReused .
                ?diaryReusedInPubReused roud-oeuvres:manuscriptHasEditorialSet ?editorialSetDiaryReusedInPubReusedListIRI .
                ?editorialSetDiaryReusedInPubReusedListIRI knora-base:valueHasListNode ?editorialSetDiaryReusedInPubReusedIRI .
                ?editorialSetDiaryReusedInPubReusedIRI rdfs:label ?editorialSetDiaryReusedInPubReused .
                FILTER(LANG(?editorialSetDiaryReusedInPubReused) = "en")
                ?diaryReusedInPubReused roud-oeuvres:hasGeneticStage ?geneticStageDiaryReusedInPubReusedListIRI .
                ?geneticStageDiaryReusedInPubReusedListIRI knora-base:valueHasListNode ?geneticStageDiaryReusedInPubReusedIRI .
                ?geneticStageDiaryReusedInPubReusedIRI rdfs:label ?geneticStageDiaryReusedInPubReused .
                FILTER(LANG(?geneticStageDiaryReusedInPubReused) = "en")
            }
        }
    } ## end publication reused and its network
            
            
            

    
    ##################### PARTS OF MAIN PUBLICATION AND THEIR NETWORKS #####################
    ## parts of main publication
    optional {
        ?mainPubPart a ?mainPubPartType .
        # to avoid blank nodes
        FILTER(STRSTARTS(str(?mainPubPartType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
        ?mainPubPart    roud-oeuvres:pubPartIsPartOf ?mainPub ;
                        roud-oeuvres:pubPartHasTitle ?mainPubPartTitleIRI ;
                        roud-oeuvres:pubPartHasNumber ?mainPubPartNumberIRI .
        ?mainPubPartTitleIRI knora-base:valueHasString ?mainPubPartTitle .
        ?mainPubPartNumberIRI knora-base:valueHasString ?mainPubPartNumber .

        ## parts of dossier of main publication
        ?dossierPartMainPub a ?dossierParMainPubtType .
        FILTER(STRSTARTS(str(?dossierParMainPubtType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
        ?dossierPartMainPub roud-oeuvres:geneticDossierPartIsPartOfGeneticDossier ?dossierMainPub ;
                            roud-oeuvres:geneticDossierPartResultsInPubPart ?mainPubPart .

        ## avant-textes of main pub parts
        optional {
            ?atMainPubPart a ?atMainPubPartType .
            # to avoid blank nodes
            FILTER(STRSTARTS(str(?atMainPubPartType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
            ?atMainPubPart roud-oeuvres:msIsAvantTextInGeneticDossierPart ?dossierPartMainPub .
            ?atMainPubPart roud-oeuvres:manuscriptHasTitle ?titleAtMainPubPartIRI .
            ?titleAtMainPubPartIRI knora-base:valueHasString ?titleAtMainPubPart .
            ?atMainPubPart roud-oeuvres:manuscriptIsInArchive ?archiveAtMainPubPartListIRI .
            ?archiveAtMainPubPartListIRI knora-base:valueHasListNode ?archiveAtMainPubPartIRI .
            ?archiveAtMainPubPartIRI rdfs:label ?archiveAtMainPubPart .
            FILTER(LANG(?archiveAtMainPubPart) = "en")
            ?atMainPubPart roud-oeuvres:manuscriptHasShelfmark ?shelfmarkAtMainPubPartIRI .
            ?shelfmarkAtMainPubPartIRI knora-base:valueHasString ?shelfmarkAtMainPubPart .
            ?atMainPubPart roud-oeuvres:manuscriptHasEditorialSet ?editorialSetAtMainPubPartListIRI .
            ?editorialSetAtMainPubPartListIRI knora-base:valueHasListNode ?editorialSetAtMainPubPartIRI .
            ?editorialSetAtMainPubPartIRI rdfs:label ?editorialSetAtMainPubPart .
            FILTER(LANG(?editorialSetAtMainPubPart) = "en")
            ?atMainPubPart roud-oeuvres:hasGeneticStage ?geneticStageAtMainPubPartListIRI .
            ?geneticStageAtMainPubPartListIRI knora-base:valueHasListNode ?geneticStageAtMainPubPartIRI .
            ?geneticStageAtMainPubPartIRI rdfs:label ?geneticStageAtMainPubPart .
            FILTER(LANG(?geneticStageAtMainPubPart) = "en")
            }   ## end avant-textes of main pub parts
        
        ## diary entries (ms) reused in main pub parts (NOT TESTED)
        optional {
            ?diaryReusedInPubPart a ?diaryReusedInPubPartType .
            # to avoid blank nodes
            FILTER(STRSTARTS(str(?diaryReusedInPubPartType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
            ?diaryReusedInPubPart roud-oeuvres:msIsReusedInDossierPart ?dossierPartMainPub .
            ?diaryReusedInPubPart roud-oeuvres:manuscriptHasTitle ?titleDiaryReusedInPubPartIRI .
            ?titleDiaryReusedInPubPartIRI knora-base:valueHasString ?titleDiaryReusedInPubPart .
            ?diaryReusedInPubPart roud-oeuvres:manuscriptIsInArchive ?archiveDiaryReusedInPubPartListIRI .
            ?archiveDiaryReusedInPubPartListIRI knora-base:valueHasListNode ?archiveDiaryReusedInPubPartIRI .
            ?archiveDiaryReusedInPubPartIRI rdfs:label ?archiveDiaryReusedInPubPart .
            FILTER(LANG(?archiveDiaryReusedInPubPart) = "en")
            ?diaryReusedInPubPart roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDiaryReusedInPubPartIRI .
            ?shelfmarkDiaryReusedInPubPartIRI knora-base:valueHasString ?shelfmarkDiaryReusedInPubPart .
            ?diaryReusedInPubPart roud-oeuvres:manuscriptHasEditorialSet ?editorialSetDiaryReusedInPubPartListIRI .
            ?editorialSetDiaryReusedInPubPartListIRI knora-base:valueHasListNode ?editorialSetDiaryReusedInPubPartIRI .
            ?editorialSetDiaryReusedInPubPartIRI rdfs:label ?editorialSetDiaryReusedInPubPart .
            FILTER(LANG(?editorialSetDiaryReusedInPubPart) = "en")
            ?diaryReusedInPubPart roud-oeuvres:hasGeneticStage ?geneticStageDiaryReusedInPubPartListIRI .
            ?geneticStageDiaryReusedInPubPartListIRI knora-base:valueHasListNode ?geneticStageDiaryReusedInPubPartIRI .
            ?geneticStageDiaryReusedInPubPartIRI rdfs:label ?geneticStageDiaryReusedInPubPart .
            FILTER(LANG(?geneticStageDiaryReusedInPubPart) = "en")
        } ## end diary entries (ms) reused in main pub parts
        
        ## diary entries (msPart) reused in main pub parts
        optional {
            ?dReusedInPubPart   a ?dReusedInPubPartType .
            FILTER(STRSTARTS(str(?dReusedInPubPartType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
            ?dReusedInPubPart   roud-oeuvres:msPartIsReusedInDossierPart ?dossierPartMainPub ;
                                roud-oeuvres:msPartHasTitle ?titleDReusedInPubPartIRI .
            ?titleDReusedInPubPartIRI knora-base:valueHasString ?titleDReusedInPubPart .
            ?dReusedInPubPart   roud-oeuvres:msPartHasNumber ?numberDReusedInPubPartIRI .
            ?numberDReusedInPubPartIRI knora-base:valueHasString ?numberDReusedInPubPart .
            ?dReusedInPubPart roud-oeuvres:msPartIsPartOf ?dReusedInPubPartMs .
            ?dReusedInPubPartMs roud-oeuvres:manuscriptHasTitle ?titleDReusedInPubPartMsIRI .
            ?titleDReusedInPubPartMsIRI knora-base:valueHasString ?titleDReusedInPubPartMs .
            ?dReusedInPubPartMs roud-oeuvres:manuscriptHasShelfmark ?shelfmarkDReusedInPubPartMsIRI .
            ?shelfmarkDReusedInPubPartMsIRI knora-base:valueHasString ?shelfmarkDReusedInPubPartMs .
            ?dReusedInPubPartMs roud-oeuvres:manuscriptIsInArchive ?archiveDReusedInPubPartMsListIRI .
            ?archiveDReusedInPubPartMsListIRI knora-base:valueHasListNode ?archiveDReusedInPubPartMsIRI .
            ?archiveDReusedInPubPartMsIRI rdfs:label ?archiveDReusedInPubPartMs .
            FILTER(LANG(?archiveDReusedInPubPartMs) = "en")
        }
        
        ## publication reusing main pub part
        optional {
            ?mainPubPart roud-oeuvres:pubPartIsReusedInDossier ?dossierPubReusingMainPubPart .
            ?dossierPubReusingMainPubPart roud-oeuvres:geneticDossierResultsInPublication ?pubReusingMainPubPart .
            ?pubReusingMainPubPart  roud-oeuvres:publicationHasTitle ?pubReusingMainPubPartTitleIRI .
            ?pubReusingMainPubPartTitleIRI knora-base:valueHasString ?pubReusingMainPubPartTitle .
            ?pubReusingMainPubPart roud-oeuvres:publicationHasDate ?pubReusingMainPubPartDateIRI .
            ?pubReusingMainPubPartDateIRI knora-base:valueHasString ?pubReusingMainPubPartDate .
            ?pubReusingMainPubPart roud-oeuvres:isPublishedInPeriodical ?pubReusingMainPubPartPeriodicalListIRI .  
            ?pubReusingMainPubPartPeriodicalListIRI roud-oeuvres:periodicalHasTitle ?pubReusingMainPubPartPeriodicalIRI .
            ?pubReusingMainPubPartPeriodicalIRI knora-base:valueHasString ?pubReusingMainPubPartPeriodical .
            optional {
                ?pubReusingMainPubPart roud-oeuvres:publicationHasPhoto ?pubReusingMainPubPartPhoto .
            }
        }
        
    } ## end pub parts and their networks
    
    
    
    ##################### RE-PUBLICATION #####################
    ?mainPub roud-oeuvres:publicationIsRepublishedDossier ?mainPubRepubDossier .
    ?mainPubRepubDossier roud-oeuvres:geneticDossierResultsInPublication ?mainPubRepub .
    ?mainPubRepub   a ?mainPubRepubType .
    FILTER(STRSTARTS(str(?mainPubRepubType), "http://www.knora.org/ontology/0112/roud-oeuvres")) .
    ?mainPubRepub roud-oeuvres:publicationHasTitle ?mainPubRepubTypeTitleIRI .
    ?mainPubRepubTypeTitleIRI knora-base:valueHasString ?mainPubRepubTypeTitle .
    ?mainPubRepub roud-oeuvres:publicationHasDate ?mainPubRepubTypeDateIRI .
    ?mainPubRepubTypeDateIRI knora-base:valueHasString ?mainPubRepubTypeDate .
            
    
    
}