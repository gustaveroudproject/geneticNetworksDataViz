PREFIX knora-api: <http://api.knora.org/ontology/knora-api/simple/v2#>
PREFIX roud-oeuvres: <http://www.knora.org/ontology/0112/roud-oeuvres#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    
   
     ## publication part of pub
    ?pubPart_1 a ?pubPart_1_type ;
             roud-oeuvres:pubPartIsPartOf ?pub_0 ;
             roud-oeuvres:pubPartHasTitle ?pubPart_1_title ;
             roud-oeuvres:pubPartHasNumber ?pubPart_1_number .


    ## pub data
    ?pub_0 a ?pub_0_type ;
            roud-oeuvres:publicationHasTitle ?pub_0_title ;
            roud-oeuvres:publicationHasDate ?pub_0_date ;
            roud-oeuvres:isPublishedInPeriodical ?pub_0_periodical ;
            # roud-oeuvres:publicationHasPhoto ?pub_0_photo ;
            roud-oeuvres:hasPublisher ?pub_0_publisher .

    
    ## pub parts dossiers
    ?pubPart_1_dossierPart a ?pubPart_1_dossierPart_type .
    ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
    
    ## publication reused in pub part (pubPart1)
    ?pub_down10 roud-oeuvres:publicationIsReusedInDossierPart ?pubPart_1_dossierPart .
    
    ## diary entries (ms) reused in pubPart (pubPart2)
    ?diaryReusedInPubPart_1   a ?diaryReusedInPubPart_1_type ;
                            roud-oeuvres:msIsReusedInDossierPart ?pubPart_1_dossierPart ;
                            roud-oeuvres:manuscriptHasTitle ?diaryReusedInPubPart_1_title ;
                            roud-oeuvres:manuscriptIsInArchive ?diaryReusedInPubPart_1_archive ;
                            roud-oeuvres:manuscriptHasShelfmark ?diaryReusedInPubPart_1_shelfmark ;
                            roud-oeuvres:manuscriptHasEditorialSet ?diaryReusedInPubPart_1_editorialSet .
    ## diary levels (ms > ms)
    ?diaryRewrittenInDiaryReusedInPubPart_1 a ?diaryRewrittenInDiaryReusedInPubPart_1_type ;
            roud-oeuvres:msIsRewrittenInMs ?diaryReusedInPubPart_1 ;
            roud-oeuvres:manuscriptHasTitle ?diaryRewrittenInDiaryReusedInPubPart_1_title ;
            roud-oeuvres:manuscriptIsInArchive ?diaryRewrittenInDiaryReusedInPubPart_1_archive ;
            roud-oeuvres:manuscriptHasShelfmark ?diaryRewrittenInDiaryReusedInPubPart_1_shelfmark ;
            roud-oeuvres:manuscriptHasEditorialSet ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet .
    ## diary levels (msPart > ms)
    ?diaryPartRewrittenInDiaryReusedInPubPart_1 a ?diaryPartRewrittenInDiaryReusedInPubPart_1_type ;
            roud-oeuvres:msPartIsRewrittenInMs ?diaryReusedInPubPart_1 ;
            roud-oeuvres:msPartHasTitle ?diaryPartRewrittenInDiaryReusedInPubPart_1_title ;
            roud-oeuvres:msPartHasNumber ?diaryPartRewrittenInDiaryReusedInPubPart_1_number ;
            roud-oeuvres:msPartIsPartOf ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms .
    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms a ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_type ;
            roud-oeuvres:manuscriptHasTitle ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_title ;
            roud-oeuvres:manuscriptIsInArchive ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archive ;
            roud-oeuvres:manuscriptHasShelfmark ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_shelfmark .

    
    ## diary entries (msPart) reused in pubPart (pubPart3)
    ?dReusedInPubPart_1   a ?dReusedInPubPart_1_type ;
                        roud-oeuvres:msPartIsReusedInDossierPart ?pubPart_1_dossierPart ;
                        roud-oeuvres:msPartHasTitle ?dReusedInPubPart_1_title ;
                        roud-oeuvres:msPartHasNumber ?dReusedInPubPart_1_number ;
                        roud-oeuvres:msPartIsPartOf ?dReusedInPubPart_1_Ms .
    ?dReusedInPubPart_1_Ms a ?dReusedInPubPart_1_Ms_type ;
                        roud-oeuvres:manuscriptHasTitle ?dReusedInPubPart_1_Ms_title ;
                        roud-oeuvres:manuscriptIsInArchive ?dReusedInPubPart_1_Ms_archive ;
                        roud-oeuvres:manuscriptHasShelfmark ?dReusedInPubPart_1_Ms_shelfmark .
    ## diary levels (ms > msPart)
    ?diaryRewrittenInDReusedInPubPart_1 a ?diaryRewrittenInDReusedInPubPart_1_type ;
            roud-oeuvres:msIsRewrittenInMsPart ?dReusedInPubPart_1 ;
            roud-oeuvres:manuscriptHasTitle ?diaryRewrittenInDReusedInPubPart_1_title ;
            roud-oeuvres:manuscriptIsInArchive ?diaryRewrittenInDReusedInPubPart_1_archive ;
            roud-oeuvres:manuscriptHasShelfmark ?diaryRewrittenInDReusedInPubPart_1_shelfmark ;
            roud-oeuvres:manuscriptHasEditorialSet ?diaryRewrittenInDReusedInPubPart_1_editorialSet .
    ## diary levels (msPart > msPart)
    ?diaryPartRewrittenInDReusedInPubPart_1 a ?diaryPartRewrittenInDReusedInPubPart_1_type ;
            roud-oeuvres:msPartIsRewrittenInMsPart ?dReusedInPubPart_1 ;
            roud-oeuvres:msPartHasTitle ?diaryPartRewrittenInDReusedInPubPart_1_title ;
            roud-oeuvres:msPartHasNumber ?diaryPartRewrittenInDReusedInPubPart_1_number ;
            roud-oeuvres:msPartIsPartOf ?diaryPartRewrittenInDReusedInPubPart_1_Ms .
    ?diaryPartRewrittenInDReusedInPubPart_1_Ms a ?diaryPartRewrittenInDReusedInPubPart_1_Ms_type ;
            roud-oeuvres:manuscriptHasTitle ?diaryPartRewrittenInDReusedInPubPart_1_Ms_title ;
            roud-oeuvres:manuscriptIsInArchive ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archive ;
            roud-oeuvres:manuscriptHasShelfmark ?diaryPartRewrittenInDReusedInPubPart_1_Ms_shelfmark .
    
    
    
    ## avant-textes of pubPart (pubPart4)
    ?atPubPart_1  a ?atPubPart_1_type ;
                roud-oeuvres:msIsAvantTextInGeneticDossierPart ?pubPart_1_dossierPart ;
                roud-oeuvres:manuscriptHasTitle ?atPubPart_1_title ;
                roud-oeuvres:manuscriptIsInArchive ?atPubPart_1_archive ;
                roud-oeuvres:manuscriptHasShelfmark ?atPubPart_1_shelfmark ;
                roud-oeuvres:hasGeneticStage ?atPubPart_1_geneticStage . 

    ## pub parts reused in pub part (pubPart5)
    ?pubPart_down11 roud-oeuvres:pubPartIsReusedInDossierPart ?pubPart_1_dossierPart .
    

    ## publication reusing pub part (pubPart7)
    ?pubPart_1 roud-oeuvres:pubPartIsReusedInDossier ?pub_up10_dossier .
    ?pub_up10_dossier roud-oeuvres:geneticDossierResultsInPublication ?pub_up10 .


    ## pub part reusing pub part (pubPart8)
    ?pubPart_1 roud-oeuvres:pubPartIsReusedInDossierPart ?pubPart_up11_dossierPart .
    ?pubPart_up11_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_up11 .
 
    
    
}

FROM <http://www.knora.org/data/0112/roud-oeuvres>
# to avoid blank nodes
FROM <http://www.ontotext.com/explicit>
WHERE {
    
    BIND(<http://rdfh.ch/0112/qat2AbggR022DPSwnUfogQ> as ?pubPart_1)    
    
    
    ## pub parts
    optional {
        ?pubPart_1 a ?pubPart_1_type ;
                        roud-oeuvres:pubPartHasTitle ?pubPart_1_titleIRI ;
                        roud-oeuvres:pubPartHasNumber ?pubPart_1_numberIRI .
        ?pubPart_1_titleIRI knora-base:valueHasString ?pubPart_1_title .
        ?pubPart_1_numberIRI knora-base:valueHasString ?pubPart_1_number .
        ?pubPart_1 knora-base:isDeleted false .
    }

    ## pub data
    ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
    ?pub_0 a ?pub_0_type .
    ?pub_0 roud-oeuvres:publicationHasTitle ?pub_0_titleIRI .
    ?pub_0_titleIRI knora-base:valueHasString ?pub_0_title .
    ?pub_0 roud-oeuvres:publicationHasDate ?pub_0_dateIRI .
    ?pub_0_dateIRI knora-base:valueHasString ?pub_0_date .
    # if article
    optional {
        ?pub_0 roud-oeuvres:isPublishedInPeriodical ?pub_0_periodicalIRI .
        ?pub_0_periodicalIRI roud-oeuvres:periodicalHasTitle ?pub_0_periodicalTitleIRI .
        ?pub_0_periodicalTitleIRI knora-base:valueHasString ?pub_0_periodical .
    }
    # if book
    optional {
        ?pub_0 roud-oeuvres:hasPublisher ?pub_0_publisherIRI .
        ?pub_0_publisherIRI roud-oeuvres:publisherHasName ?pub_0_publisherNameIRI .
        ?pub_0_publisherNameIRI knora-base:valueHasString ?pub_0_publisher .
    }
    optional {
        ?pub_0 roud-oeuvres:publicationHasPhoto ?pub_0_photo .
    }


    ## pub parts dossiers
    optional {
        ?pubPart_1_dossierPart a ?pubPart_1_dossierPart_type .
        # remove because pub_0_dossier and its type is not shown anymore in results if this exists ... weird!
        # ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartIsPartOfGeneticDossier ?pub_0_dossier .
        # ?pub_0_dossier roud-oeuvres:geneticDossierResultsInPublication ?pub_0 .
        ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
        ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
        ?pubPart_1_dossierPart knora-base:isDeleted false .
        
        
        ## publication reused in pub part (1)
        optional {
            ?pub_down10  roud-oeuvres:publicationIsReusedInDossierPart ?pubPart_1_dossierPart .
            ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
            ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
            ?pub_down10 knora-base:isDeleted false .
        }

    ## diary entries (ms) reused in pubPart (2)
            optional {
                ?diaryReusedInPubPart_1 a ?diaryReusedInPubPart_1_type .
                ?diaryReusedInPubPart_1 roud-oeuvres:msIsReusedInDossierPart ?pubPart_1_dossierPart .
                ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
            	?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
                ?diaryReusedInPubPart_1 knora-base:isDeleted false .
                ?diaryReusedInPubPart_1 roud-oeuvres:manuscriptHasTitle ?diaryReusedInPubPart_1_titleIRI .
                ?diaryReusedInPubPart_1_titleIRI knora-base:valueHasString ?diaryReusedInPubPart_1_title .
                ?diaryReusedInPubPart_1 roud-oeuvres:manuscriptIsInArchive ?diaryReusedInPubPart_1_archiveListIRI .
                ?diaryReusedInPubPart_1_archiveListIRI knora-base:valueHasListNode ?diaryReusedInPubPart_1_archiveIRI .
                ?diaryReusedInPubPart_1_archiveIRI rdfs:label ?diaryReusedInPubPart_1_archive .
                FILTER(LANG(?diaryReusedInPubPart_1_archive) = "en")
                ?diaryReusedInPubPart_1 roud-oeuvres:manuscriptHasShelfmark ?diaryReusedInPubPart_1_shelfmarkIRI .
                ?diaryReusedInPubPart_1_shelfmarkIRI knora-base:valueHasString ?diaryReusedInPubPart_1_shelfmark .
                ?diaryReusedInPubPart_1 roud-oeuvres:manuscriptHasEditorialSet ?diaryReusedInPubPart_1_editorialSetListIRI .
                ?diaryReusedInPubPart_1_editorialSetListIRI knora-base:valueHasListNode ?diaryReusedInPubPart_1_editorialSetIRI .
                ?diaryReusedInPubPart_1_editorialSetIRI rdfs:label ?diaryReusedInPubPart_1_editorialSet .
                FILTER(LANG(?diaryReusedInPubPart_1_editorialSet) = "en")
                ## diary levels (ms > ms)
                optional {                
                    ?diaryRewrittenInDiaryReusedInPubPart_1     a ?diaryRewrittenInDiaryReusedInPubPart_1_type .
                    ?diaryRewrittenInDiaryReusedInPubPart_1     roud-oeuvres:msIsRewrittenInMs ?diaryReusedInPubPart_1 .
                    ?diaryReusedInPubPart_1 roud-oeuvres:msIsReusedInDossierPart ?pubPart_1_dossierPart .
                    ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
                    ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
                    ?diaryRewrittenInDiaryReusedInPubPart_1     knora-base:isDeleted false .
                    ?diaryRewrittenInDiaryReusedInPubPart_1     roud-oeuvres:manuscriptHasTitle ?diaryRewrittenInDiaryReusedInPubPart_1_title_IRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_title_IRI           knora-base:valueHasString ?diaryRewrittenInDiaryReusedInPubPart_1_title .
                    ?diaryRewrittenInDiaryReusedInPubPart_1     roud-oeuvres:manuscriptIsInArchive ?diaryRewrittenInDiaryReusedInPubPart_1_archive_ListIRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_archive_ListIRI     knora-base:valueHasListNode ?diaryRewrittenInDiaryReusedInPubPart_1_archive_IRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_archive_IRI         rdfs:label ?diaryRewrittenInDiaryReusedInPubPart_1_archive .
                    FILTER(LANG(?diaryRewrittenInDiaryReusedInPubPart_1_archive) = "en")
                    ?diaryRewrittenInDiaryReusedInPubPart_1     roud-oeuvres:manuscriptHasShelfmark ?diaryRewrittenInDiaryReusedInPubPart_1_shelfmark_IRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_shelfmark_IRI       knora-base:valueHasString ?diaryRewrittenInDiaryReusedInPubPart_1_shelfmark .
                    ?diaryRewrittenInDiaryReusedInPubPart_1     roud-oeuvres:manuscriptHasEditorialSet ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet_ListIRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet_ListIRI    knora-base:valueHasListNode ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet_IRI .
                    ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet_IRI    rdfs:label ?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet .
                    FILTER(LANG(?diaryRewrittenInDiaryReusedInPubPart_1_editorialSet) = "en")
                }
                ## diary levels (msPart > ms)
                optional {
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1     a ?diaryPartRewrittenInDiaryReusedInPubPart_1_type ;
                            roud-oeuvres:msPartIsRewrittenInMs  ?diaryReusedInPubPart_1 .
                    ?diaryReusedInPubPart_1 roud-oeuvres:msIsReusedInDossierPart ?pubPart_1_dossierPart .
                    ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
                    ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1     knora-base:isDeleted false .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1 roud-oeuvres:msPartHasTitle ?diaryPartRewrittenInDiaryReusedInPubPart_1_title_IRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_title_IRI knora-base:valueHasString ?diaryPartRewrittenInDiaryReusedInPubPart_1_title .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1   roud-oeuvres:msPartHasNumber ?diaryPartRewrittenInDiaryReusedInPubPart_1_number_IRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_number_IRI knora-base:valueHasString ?diaryPartRewrittenInDiaryReusedInPubPart_1_number .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1 roud-oeuvres:msPartIsPartOf ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms a ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_type .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasTitle ?diaryPartRewrittenInDiaryReusedInPubPart_1_MsIRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_MsIRI knora-base:valueHasString ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_title .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasShelfmark ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_shelfmarkIRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_shelfmarkIRI knora-base:valueHasString ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_shelfmark .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms roud-oeuvres:manuscriptIsInArchive ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archiveListIRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archiveListIRI knora-base:valueHasListNode ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archiveIRI .
                    ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archiveIRI rdfs:label ?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archive .
                    FILTER(LANG(?diaryPartRewrittenInDiaryReusedInPubPart_1_Ms_archive) = "en")
                }
            } 

        ## diary entries (msPart) reused in pubPart (3)
            optional {
                ?dReusedInPubPart_1   a ?dReusedInPubPart_1_type .
                ?dReusedInPubPart_1 knora-base:isDeleted false .
                ?dReusedInPubPart_1   roud-oeuvres:msPartIsReusedInDossierPart ?pubPart_1_dossierPart ;
                                    roud-oeuvres:msPartHasTitle ?dReusedInPubPart_1_titleIRI .
                ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
            	?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
                ?dReusedInPubPart_1_titleIRI knora-base:valueHasString ?dReusedInPubPart_1_title .
                ?dReusedInPubPart_1   roud-oeuvres:msPartHasNumber ?dReusedInPubPart_1_numberIRI .
                ?dReusedInPubPart_1_numberIRI knora-base:valueHasString ?dReusedInPubPart_1_number .
                ?dReusedInPubPart_1 roud-oeuvres:msPartIsPartOf ?dReusedInPubPart_1_Ms .
                ?dReusedInPubPart_1_Ms a ?dReusedInPubPart_1_Ms_type .
                ?dReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasTitle ?dReusedInPubPart_1_Ms_titleIRI .
                ?dReusedInPubPart_1_Ms_titleIRI knora-base:valueHasString ?dReusedInPubPart_1_Ms_title .
                ?dReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasShelfmark ?dReusedInPubPart_1_Ms_shelfmarkIRI .
                ?dReusedInPubPart_1_Ms_shelfmarkIRI knora-base:valueHasString ?dReusedInPubPart_1_Ms_shelfmark .
                ?dReusedInPubPart_1_Ms roud-oeuvres:manuscriptIsInArchive ?dReusedInPubPart_1_Ms_archiveListIRI .
                ?dReusedInPubPart_1_Ms_archiveListIRI knora-base:valueHasListNode ?dReusedInPubPart_1_Ms_archiveIRI .
                ?dReusedInPubPart_1_Ms_archiveIRI rdfs:label ?dReusedInPubPart_1_Ms_archive .
                FILTER(LANG(?dReusedInPubPart_1_Ms_archive) = "en")
                ## diary levels (ms > msPart)
                optional {                
                    ?diaryRewrittenInDReusedInPubPart_1     a   ?diaryRewrittenInDReusedInPubPart_1_type .
                    ?diaryRewrittenInDReusedInPubPart_1     roud-oeuvres:msIsRewrittenInMsPart  ?dReusedInPubPart_1 .
                    ?dReusedInPubPart_1   roud-oeuvres:msPartIsReusedInDossierPart ?pubPart_1_dossierPart .
                    ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
                    ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .                    
                    ?diaryRewrittenInDReusedInPubPart_1     knora-base:isDeleted false .
                    ?diaryRewrittenInDReusedInPubPart_1     roud-oeuvres:manuscriptHasTitle ?diaryRewrittenInDReusedInPubPart_1_title_IRI .
                    ?diaryRewrittenInDReusedInPubPart_1_title_IRI           knora-base:valueHasString ?diaryRewrittenInDReusedInPubPart_1_title .
                    ?diaryRewrittenInDReusedInPubPart_1     roud-oeuvres:manuscriptIsInArchive ?diaryRewrittenInDReusedInPubPart_1_archive_ListIRI .
                    ?diaryRewrittenInDReusedInPubPart_1_archive_ListIRI     knora-base:valueHasListNode ?diaryRewrittenInDReusedInPubPart_1_archive_IRI .
                    ?diaryRewrittenInDReusedInPubPart_1_archive_IRI         rdfs:label ?diaryRewrittenInDReusedInPubPart_1_archive .
                    FILTER(LANG(?diaryRewrittenInDReusedInPubPart_1_archive) = "en")
                    ?diaryRewrittenInDReusedInPubPart_1     roud-oeuvres:manuscriptHasShelfmark ?diaryRewrittenInDReusedInPubPart_1_shelfmark_IRI .
                    ?diaryRewrittenInDReusedInPubPart_1_shelfmark_IRI       knora-base:valueHasString ?diaryRewrittenInDReusedInPubPart_1_shelfmark .
                    ?diaryRewrittenInDReusedInPubPart_1     roud-oeuvres:manuscriptHasEditorialSet ?diaryRewrittenInDReusedInPubPart_1_editorialSet_ListIRI .
                    ?diaryRewrittenInDReusedInPubPart_1_editorialSet_ListIRI    knora-base:valueHasListNode ?diaryRewrittenInDReusedInPubPart_1_editorialSet_IRI .
                    ?diaryRewrittenInDReusedInPubPart_1_editorialSet_IRI    rdfs:label ?diaryRewrittenInDReusedInPubPart_1_editorialSet .
                    FILTER(LANG(?diaryRewrittenInDReusedInPubPart_1_editorialSet) = "en")
                }
                ## diary levels (msPart > msPart)
                optional {
                    ?diaryPartRewrittenInDReusedInPubPart_1     a ?diaryPartRewrittenInDReusedInPubPart_1_type ;
                                        roud-oeuvres:msPartIsRewrittenInMsPart  ?dReusedInPubPart_1 .
                    ?dReusedInPubPart_1   roud-oeuvres:msPartIsReusedInDossierPart ?pubPart_1_dossierPart .
                    ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
                    ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 . 
                    ?diaryPartRewrittenInDReusedInPubPart_1     knora-base:isDeleted false .
                    ?diaryPartRewrittenInDReusedInPubPart_1 roud-oeuvres:msPartHasTitle ?diaryPartRewrittenInDReusedInPubPart_1_title_IRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_title_IRI knora-base:valueHasString ?diaryPartRewrittenInDReusedInPubPart_1_title .
                    ?diaryPartRewrittenInDReusedInPubPart_1   roud-oeuvres:msPartHasNumber ?diaryPartRewrittenInDReusedInPubPart_1_number_IRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_number_IRI knora-base:valueHasString ?diaryPartRewrittenInDReusedInPubPart_1_number .
                    ?diaryPartRewrittenInDReusedInPubPart_1 roud-oeuvres:msPartIsPartOf ?diaryPartRewrittenInDReusedInPubPart_1_Ms .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms a ?diaryPartRewrittenInDReusedInPubPart_1_Ms_type .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasTitle ?diaryPartRewrittenInDReusedInPubPart_1_MsIRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_MsIRI knora-base:valueHasString ?diaryPartRewrittenInDReusedInPubPart_1_Ms_title .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms roud-oeuvres:manuscriptHasShelfmark ?diaryPartRewrittenInDReusedInPubPart_1_Ms_shelfmarkIRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms_shelfmarkIRI knora-base:valueHasString ?diaryPartRewrittenInDReusedInPubPart_1_Ms_shelfmark .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms roud-oeuvres:manuscriptIsInArchive ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archiveListIRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archiveListIRI knora-base:valueHasListNode ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archiveIRI .
                    ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archiveIRI rdfs:label ?diaryPartRewrittenInDReusedInPubPart_1_Ms_archive .
                    FILTER(LANG(?diaryPartRewrittenInDReusedInPubPart_1_Ms_archive) = "en")
                }
            }
      
        
    ## avant-textes of pubPart (4)
        optional {
            ?atPubPart_1 a ?atPubPart_1_type .
            ?atPubPart_1 knora-base:isDeleted false .
            ?atPubPart_1 roud-oeuvres:msIsAvantTextInGeneticDossierPart ?pubPart_1_dossierPart .
            ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
            ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
            ?atPubPart_1 roud-oeuvres:manuscriptHasTitle ?atPubPart_1_titleIRI .
            ?atPubPart_1_titleIRI knora-base:valueHasString ?atPubPart_1_title .
            ?atPubPart_1 roud-oeuvres:manuscriptIsInArchive ?atPubPart_1_archiveListIRI .
            ?atPubPart_1_archiveListIRI knora-base:valueHasListNode ?atPubPart_1_archiveIRI .
            ?atPubPart_1_archiveIRI rdfs:label ?atPubPart_1_archive .
            FILTER(LANG(?atPubPart_1_archive) = "en")
            ?atPubPart_1 roud-oeuvres:manuscriptHasShelfmark ?atPubPart_1_shelfmarkIRI .
            ?atPubPart_1_shelfmarkIRI knora-base:valueHasString ?atPubPart_1_shelfmark .
            ?atPubPart_1 roud-oeuvres:hasGeneticStage ?atPubPart_1_geneticStageListIRI .
            ?atPubPart_1_geneticStageListIRI knora-base:valueHasListNode ?atPubPart_1_geneticStageIRI .
            ?atPubPart_1_geneticStageIRI rdfs:label ?atPubPart_1_geneticStage .
            FILTER(LANG(?atPubPart_1_geneticStage) = "en")
        }  
        
    ## pub parts reused in pub part (5)
        optional {
            ?pubPart_down11 roud-oeuvres:pubPartIsReusedInDossierPart ?pubPart_1_dossierPart .
            ?pubPart_1_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_1 .
            ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
            ?pubPart_down11 knora-base:isDeleted false .
        }

    } ## end optional dossier of pub part
    
        
    ## publication reusing pub part (7)
    optional {
        ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
        ?pubPart_1 roud-oeuvres:pubPartIsReusedInDossier ?pub_up10_dossier .
        ?pub_up10_dossier roud-oeuvres:geneticDossierResultsInPublication ?pub_up10 .
        ?pub_up10 knora-base:isDeleted false .
    }
        
    ## pub part reusing pub part (8)
    optional {
        ?pubPart_1    roud-oeuvres:pubPartIsPartOf ?pub_0 .
        ?pubPart_1 roud-oeuvres:pubPartIsReusedInDossierPart ?pubPart_up11_dossierPart .
        ?pubPart_up11_dossierPart roud-oeuvres:geneticDossierPartResultsInPubPart ?pubPart_up11 .
        ?pubPart_up11 knora-base:isDeleted false .
    }   
        
    } 